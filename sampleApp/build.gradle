apply plugin: 'com.android.application'
//apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'realm-android'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "com.zeyad.usecase.accesslayer"
        minSdkVersion 17
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        releaseConfig {
            storeFile file('../keystore/keystore.jks')
            storePassword KEYSTORE_PASSWORD
            keyAlias "Zeyad"
            keyPassword KEYSTORE_PASSWORD
        }

        debugConfig {
            storeFile file('../keystore/keystore.jks')
            storePassword KEYSTORE_PASSWORD
            keyAlias "Zeyad"
            keyPassword KEYSTORE_PASSWORD
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), "$project.rootDir/tools/rules-proguard-debug.pro"
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debugConfig
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
//, 'proguard-fresco.pro'
            signingConfig signingConfigs.releaseConfig
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        //lint rules of conduct
        warningsAsErrors true
        abortOnError true
        htmlReport true
        //locations for the rules and output
        lintConfig file("${rootDir}/config/lint/lint-config.xml")
        htmlOutput file("${buildDir}/reports/lint/lint.html")
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.1'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'  // butterknife
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/rxjava.properties'
    }
}

ext {
    supportLibraryVersion = '27.0.1'
    googlePlayVersion = '10.0.1'
    butterKnifeVersion = '8.4.0'
    rxbindingVersion = '2.0.0'
    rxLifeCycle = '2.0.1'
    parcelerVersion = '1.1.1'
    leakCanaryVersion = '1.5.1'
    androidSupportTest = '0.5'
    espressoCore = '2.2.2'
    archComp = '1.1.1'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':usecases')
    // Support
//    implementation 'com.android.support:multidex:1.0.1'
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:support-v4:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "com.android.support:palette-v7:$supportLibraryVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'


    implementation "android.arch.lifecycle:runtime:$archComp"
    implementation "android.arch.lifecycle:extensions:$archComp"
    annotationProcessor "android.arch.lifecycle:compiler:$archComp"
    implementation "android.arch.persistence.room:runtime:$archComp"
    annotationProcessor "android.arch.persistence.room:compiler:$archComp"
    // Network
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    // Rx
    implementation "com.jakewharton.rxbinding2:rxbinding:$rxbindingVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7:$rxbindingVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-design:$rxbindingVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:$rxbindingVersion"
    implementation "com.trello.rxlifecycle2:rxlifecycle:$rxLifeCycle"
    implementation "com.trello.rxlifecycle2:rxlifecycle-components:$rxLifeCycle"
    // Injection
    implementation "com.jakewharton:butterknife:$butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnifeVersion"
    // Utilities
    implementation "org.parceler:parceler-api:$parcelerVersion"
    annotationProcessor "org.parceler:parceler:$parcelerVersion"
    implementation("io.flowup:android-sdk:0.2.4") {
        exclude group: 'com.google.android.gms'
    }
    testImplementation 'io.flowup:android-sdk-no-op:0.2.4'
    // Testing
    androidTestImplementation "com.android.support.test:runner:$androidSupportTest"
    androidTestImplementation "com.android.support.test:rules:$androidSupportTest"
    androidTestImplementation "com.android.support:support-annotations:$supportLibraryVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoCore"
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation 'org.mockito:mockito-core:1.10.19'
    androidTestImplementation 'com.google.dexmaker:dexmaker:1.2'
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'
//    testImplementation 'org.mockito:mockito-core:2.0.57-beta'
}

apply from: "$project.rootDir/tools/script-git-version.gradle"
apply from: "$project.rootDir/tools/script-findbugs.gradle"
apply from: "$project.rootDir/tools/script-pmd.gradle"
apply from: "$project.rootDir/tools/script-java-code-coverage.gradle"
apply from: "$project.rootDir/tools/script-infer.gradle"
apply from: "$project.rootDir/tools/script-check-style.gradle"
