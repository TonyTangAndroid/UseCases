buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.6.1'
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    defaultConfig {
        applicationId "com.zeyad.usecase.accesslayer"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.zeyad.usecases.app.UseCasesTestRunner"
    }

    dexOptions {
        preDexLibraries true
        maxProcessCount 8
        javaMaxHeapSize "3g"
    }

    signingConfigs {
        releaseConfig {
            storeFile file('../keystore/keystore.jks')
            storePassword KEYSTORE_PASSWORD
            keyAlias "Zeyad"
            keyPassword KEYSTORE_PASSWORD
        }

        debugConfig {
            storeFile file('../keystore/keystore.jks')
            storePassword KEYSTORE_PASSWORD
            keyAlias "Zeyad"
            keyPassword KEYSTORE_PASSWORD
        }
    }

    buildTypes {
        debug {
            ext.alwaysUpdateBuildId = false
            minifyEnabled false
            shrinkResources false
            debuggable true
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), "$project.rootDir/tools/rules-proguard-debug.pro"
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debugConfig
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
    }

    productFlavors {
        dev {
            minSdkVersion 21 // To avoid using legacy multidex, set minSdkVersion to 21 or higher.
//            versionNameSuffix "-dev"
//            applicationIdSuffix '.dev'
            resConfigs "en", "xxhdpi"
            ext.alwaysUpdateBuildId = false
        }

        prod {
            // If you've configured the defaultConfig block for the release version of
            // your app, you can leave this block empty and Gradle uses configurations in
            // the defaultConfig block instead. You still need to create this flavor.
            // Otherwise, all variants use the "dev" flavor configurations.
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        //lint rules of conduct
        warningsAsErrors true
        abortOnError false
        htmlReport true
        //locations for the rules and output
        lintConfig file("${rootDir}/config/lint/lint-config.xml")
        htmlOutput file("${buildDir}/reports/lint/lint.html")
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.1'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'  // butterknife
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/rxjava.properties'
    }

    if (project.hasProperty('devBuild')) {
        splits.abi.enable = false
        splits.density.enable = false
        aaptOptions.cruncherEnabled = false
    }

    sourceSets {
        main {
            assets.srcDirs = ['src/main/assets', 'src/androidTest/java/com/zeyad/usecases/app/assets/']
        }
    }
}

ext {
    supportLibraryVersion = '25.3.1'
    butterKnifeVersion = '8.6.0'
    rxbindingVersion = '2.0.0'
    rxLifeCycle = '2.0.1'
    parcelerVersion = '1.1.1'
    leakCanaryVersion = '1.5.1'
    androidSupportTest = '0.5'
    espressoCore = '2.2.2'
    archCompVersion = '1.0.0-alpha5'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':usecases')
    // Support
//    compile 'com.android.support:multidex:1.0.1'
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:support-v4:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support:cardview-v7:$supportLibraryVersion"
    compile "com.android.support:palette-v7:$supportLibraryVersion"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    // Bootstrap
//    compile 'com.github.Zeyad-37:GenericRecyclerViewAdapter:master-SNAPSHOT'
    compile 'com.github.Zeyad-37:GenericRecyclerViewAdapter:1.3.0'
    compile 'com.github.Zeyad-37:RxRedux:1.3.0'

//    compile "android.arch.lifecycle:runtime:$archCompVersion"
    compile "android.arch.lifecycle:extensions:$archCompVersion"
    compile "android.arch.lifecycle:reactivestreams:$archCompVersion"
    // Network
    compile 'com.github.bumptech.glide:glide:3.7.0'
    // Rx
//    def withoutRx = { exclude group: "io.reactivex.rxjava2" }
    compile "com.jakewharton.rxbinding2:rxbinding:$rxbindingVersion"//, withoutRx
    compile "com.jakewharton.rxbinding2:rxbinding-appcompat-v7:$rxbindingVersion"
    compile "com.jakewharton.rxbinding2:rxbinding-design:$rxbindingVersion"
    compile "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:$rxbindingVersion"
    compile "com.trello.rxlifecycle2:rxlifecycle:$rxLifeCycle"
    compile "com.trello.rxlifecycle2:rxlifecycle-components:$rxLifeCycle"
    // Injection
    compile "com.jakewharton:butterknife:$butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnifeVersion"
    // Utilities
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    compile "org.parceler:parceler-api:$parcelerVersion"
    annotationProcessor "org.parceler:parceler:$parcelerVersion"
//    compile("io.flowup:android-sdk:0.2.4") {
//        exclude group: 'com.google.android.gms'
//    }
//    testCompile 'io.flowup:android-sdk-no-op:0.2.4'
    // Testing
    androidTestCompile "com.android.support.test:runner:$androidSupportTest"
    androidTestCompile "com.android.support.test:rules:$androidSupportTest"
    androidTestCompile "com.android.support:support-annotations:$supportLibraryVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-core:$espressoCore"
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestCompile('com.jakewharton.espresso:okhttp3-idling-resource:1.0.0') {
        exclude group: 'com.android.support'
    }
    androidTestCompile 'com.squareup.okhttp3:mockwebserver:3.8.0'
    androidTestCompile 'com.github.andrzejchm.RESTMock:android:0.1.4'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

//apply from: "$project.rootDir/tools/script-git-version.gradle"
apply from: "$project.rootDir/tools/script-findbugs.gradle"
//apply from: "$project.rootDir/tools/script-pmd.gradle"
//apply from: "$project.rootDir/tools/script-java-code-coverage.gradle"
//apply from: "$project.rootDir/tools/script-infer.gradle"
//apply from: "$project.rootDir/tools/script-check-style.gradle"
